START               ::=  { DECL-STMT | FUNCTION-DECL }

VARIABLE            ::=  ID [ ACCESSES ] { ( '.' VARIABLE ) | ( '->' VARIABLE) }

FUNCTION-DECL            ::=  'preparar magia de ' TYPE ID ', componentes:' ARGS STMT-BLOCK

FUNCTION-SIGNATURE  ::=  'preparar magia de ' TYPE ID ', componentes:' ARGS '...'

FUNCTION-CALL       ::=  'conjurar' ID ' [' PARAMS ']'

ARGS                ::=  [ ARG { ',' ARG } ]

ARG                 ::=  TYPE ID

PARAMS              ::=  [ PARAM { ',' PARAM } ]

PARAM               ::=  EXPR

STMT-BLOCK          ::=  '_/' STMTS '\_'

STMTS               ::=  { STMT }

STMT                ::=   NULL-STMT
                      |   RETURN-STMT
                      |   CONTINUE-STMT
                      |   BREAK-STMT
                      |   IF-STMT
                      |   WHILE-STMT
                      |   FOR-STMT
                      |   GOTO-STMT
                      |   SWITCH-STMT  
                      |   CASE-STMT
                      |   DECL-STMT

NULL-STMT           ::=  '...'

RETURN-STMT         ::=  'regressum ' EXPR '...'

CONTINUE-STMT       ::=  'manter profecia' '...'

BREAK-STMT          ::=  'cumprir profecia' '...'

IF-STMT             ::=  'Ponderar se ' EXPR '...' STMT-BLOCK { 'ou optar por ' [ 'Ponderar se' EXPR '...' ] STMT-BLOCK }

WHILE-STMT          ::=  'Dilatar tempo enquanto ' EXPR '...' STMT-BLOCK

FOR-STMT            ::=  'Fraturar tempo se ' EXPR ',' EXPR '...' STMT-BLOCK

GOTO-STMT           ::=  'translocar para ' ID '...'

SWITCH-STMT         ::=  'Iniciar profecia de ' EXPR '...' SWITCH-BLOCK

SWITCH-BLOCK        ::=  '_/' SWITCH-STMTS '\_'

SWITCH-STMTS        ::=  { CASE CASE-STMT } [ 'caso trivial, ' CASE-STMT]

CASE                ::=  'Profetizar que se  ' EXPR ', '

CASE-STMT           ::=  { STMT } BREAK-STMT

DECL-STMT           ::= TYPE-DEFINITION '...'
                      |  ASSIGNMENTS '...'
                      |  FUNCTION-SIGNATURE '...'

PLUS                ::= 'fundido a '
MINUS               ::= 'extraido de '
TIMES               ::= 'replicado por '
DIV                 ::= 'fragmentado em '
MOD                 ::= 'transmoglifado por ' 

GT                  ::= 'superior a '
LT                  ::= 'inferior a '
GE                  ::= 'superior ou equivalente a '
LE                  ::= 'inferior ou equivalente a '
EQ                  ::= 'equivalente a '
NE                  ::= 'distinto de '

AND                 ::= 'assim como '
OR                  ::= 'ou entao '
NOT                 ::= 'contrariando '

EXPR                ::=  TERM { OPERATOR TERM }

TERM                ::=  FACTOR { OPERATOR FACTOR }

FACTOR              ::=  '[' EXPR ']'
                      |  EXPR
                      |  VARIABLE
                      |  INTEGER
                      |  RATIONAL
                      |  BOOL
                      |  LITERAL
                      |  FUNCTION-CALL
                      |  TERNARY-EXPR
                      |  UNARY-EXPR
                      
OPERATOR            ::=  ARIT | REL | LOGIC

ARIT                ::=  PLUS
                      |  MINUS
                      |  TIMES
                      |  DIV
                      |  MOD

REL                 ::=  GT
                      |  LT
                      |  GE
                      |  LE
                      |  EQ
                      |  NE

LOGIC               ::=  AND
                      |  OR
                      |  NOT

TERNARY-EXPR        ::=  EXPR 'procede entao ' TERM 'caso contrario ' TERM

UNARY-EXPR          ::= UNARY-POS-MINUS
                      |  UNARY-POS-SUM
                      |  UNARY-DEREF
                      |  UNARY-REF
                      |  UNARY-NOT

UNARY-POS-MINUS    	::= "desencantar " VARIABLE
UNARY-POS-SUM     	::= "encantar" VARIABLE
UNARY-DEREF       	::= 'alma de ' VARIABLE
UNARY-REF          	::= 'lembraça de ' VARIABLE
UNARY-NOT          	::= 'contrariando ' VARIABLE  

TYPE                ::=  'Runa'
                      |  'Glifo'
                      |  'Arquiglifo'
                      |  'Semiglifo'
                      |  'Fractal'
                      |  'Arquifractal'
                      |  'SemiFractal'
                      |  'Nulo'
                      |  'Axioma'
                      |  ENUM-DEFINITION
                      |  STRUCT-DEFINITION
                      |  UNION-DEFINITION
                      |  TYPE-ENUM
                      |  TYPE-STRUCT
                      |  TYPE-UNION

TYPE-QUALIFIER      ::=  'imutavel'
                      |  'volatil'

ENUM-DEFINITION     ::=  'glifos' ' em ' ID '_/' ENUM-FIELDS '\_'

ENUM-FIELDS         ::=  ENUM-FIELD { ',' ENUM-FIELD }

ENUM-FIELD          ::=  ID [ 'será ' DEC-DIGIT ]

STRUCT-DEFINITION   ::=  'componentizacao' ' em ' [ ID ] '_/' STRUCT-FIELDS '\_'

STRUCT-FIELDS       ::=  STRUCT-FIELD { '...' STRUCT-FIELD }

STRUCT-FIELD        ::=  TYPE-DECL ID

UNION-DEFINITION    ::=  'conjuntura' ' em ' ID '_/' UNION-FIELDS '\_'

UNION-FIELDS        ::=  UNION-FIELD { '...' UNION-FIELD }

UNION-FIELD         ::=  TYPE-DECL ID

TYPE-DEFINITION     ::=  'transmutar ' TYPE ID

TYPE-ENUM           ::=  'glifos' ID

TYPE-STRUCT         ::=  'componentizacao' ID

TYPE-UNION          ::=  'conjuntura' ID

TYPE-DECL           ::=  [ 'essencia de' ] TYPE [ TYPE-QUALIFIER ]


ASSIGNMENTS         ::=  ASSIGNMENT { ', ' ASSIGNMENT }

ASSIGNMENT          ::=  [ TYPE-DECL ] VARIABLE [ 'será' EXPR ]

ACCESSES            ::=  { ACCESS }

ACCESS              ::=  '<' EXPR '>' 





NON-ZERO            ::=  '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'

DIGIT               ::=  '0' | NON-ZERO

DEC-DIGIT           ::=  DIGIT { DIGIT }

INTEGER             ::=  DEC-DIGIT

RATIONAL            ::=  DEC-DIGIT '.' DEC-DIGIT

LOWER-CASE          ::=  'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v' | 'w' | 'x' | 'y' | 'z'

UPPER-CASE          ::=  'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J' | 'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T' | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z'

SPECIAL-CHAR        ::=  '!' | '@' | '#' | '$' | '%' | '^' | '&' | '*' | '(' | ')' | '-' | '_' | '+' | '=' | '{' | '}' | '[' | ']' | '|' | '\' | ':' | ';' | "'" | '<' | '>' | ',' | '.' | '?' | '/' | '~' | '`'

ACCENT              ::=  'á' | 'é' | 'í' | 'ó' | 'ú' | 'â' | 'ê' | 'î' | 'ô' | 'û' | 'ã' | 'õ' | 'ç'

LETTER              ::=  LOWER-CASE | UPPER-CASE | ACCENT

CHAR                ::=  LETTER | DIGIT | SPECIAL-CHAR

ID                  ::=  ( LETTER | '_' ) { LETTER | DIGIT | '_' }

TRUE                ::=  'veritas'

FALSE               ::=  'falsum'

BOOL                ::=  TRUE | FALSE

NULL-CONST          ::=  'Vazio'

STR-CONST           ::=  '"' { CHAR } '"'

CHAR-CONST          ::=  "'" CHAR "'"

LITERAL             ::=  STR-CONST | CHAR-CONST | NULL-CONST